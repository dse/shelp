#!/usr/bin/env perl
use warnings;
use strict;

# .NAME
#     shelpfmt - underlying formatting engine for shelp
# .SYNOPSIS
#     shelpfmt
# .DESCRIPTION
#     shelpfmt takes its input and sends a more nicely
#     formatted version of it to stdout.
#
#     It is the underlying formatter used by shelp.

our $mode = 0;
our $blank = 0;
our $started = 0;

while (<>) {
    s{\R\z}{};

    # line that is neither a comment nor blank ends shelp section
    if ($mode && /^ *[^#]/) {
        print("\n") if !$blank;
        $blank = 1;
        $mode = 0;
        next;
    }

    # line starting with # followed by . starts a shelp section
    if (/^ *# *\. */) {
        if (!$started) {
            $started = 1;
        } else {
            print("\n") if !$blank;
        }
        $mode = 1;
        s/^ *\# *\. */\n/;
        if (/^(?<heading>.*?)[ ]+-[ ]+(?<subheading>.*)$/) {
            printf("%s - %s\n", uc($+{heading}), $+{subheading});
        } else {
            printf("%s\n", uc($_));
        }
        $blank = 1;
        next;
    }

    # blank lines and blank comment lines separate section names
    # and/or paragraphs
    if ($mode && (/^ *# *$/ || /^ *$/)) {
        print("\n") if !$blank;
        $blank = 1;
        next;
    }

    if ($mode && /^ *# ?/) {
        s/^ *\# ?/    /;
        print "$_\n";
        $blank = 0;
        next;
    }
}

print("\n") if !$blank;
